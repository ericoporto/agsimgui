// room script file

Dictionary* SpokenStrings;
String keys[128];
String values[128];
String character_name[128];
int key_count;

void SayB(this Character*, String text){
  String text_to_speak;
  if(!SpokenStrings.Contains(String.Format("%s##%s", this.Name, text))) {
    text_to_speak = text;
    SpokenStrings.Set(String.Format("%s##%s", this.Name, text), String.Format("%d",key_count));
    character_name[key_count] = this.Name;
    keys[key_count] = text.Copy();
    values[key_count] = text.Copy();
    key_count++;
  } else {
    String index = SpokenStrings.Get(String.Format("%s##%s", this.Name, text));
    text_to_speak = values[index.AsInt];
  }
  
  this.Say(text_to_speak);
}

function room_RepExec()
{   
  AgsImGui.NewFrame();  
  AgsImGui.BeginWindow("My own window");
  
  AgsImGui.Text("Talk and inspect things in the room.");
  AgsImGui.Text("You can rewrite what is said!");
  AgsImGui.Separator();

  if(key_count>0){
    for(int i=0; i < key_count; i++){
      String key = keys[i];
      String value = values[i];
      
      AgsImGui.Text(key);
      
      String result = AgsImGui.InputText(String.Format("%d %s", i, character_name[i]), value,  256);
      if(result!=null){
        values[i] = result;
      }
    }
  }
  
  AgsImGui.Separator();
  AgsImGui.Text("This is an example of possible Editors to build with AgsImGui!");
  
  AgsImGui.EndWindow();
  
  if(key_count > 3){
    AgsImGui.SetNextWindowPos(16, 16, ImGuiCond_Always);
    AgsImGui.BeginWindow("End Demo",false, eImGuiWindowFlags_NoTitleBar);
    if(AgsImGui.Button("Click Here to End Game")) QuitGame(0);
    AgsImGui.EndWindow();
  }
  
  AgsImGui.Render();
}

function room_Load()
{
  Game.Camera.Width = 320;
  Game.Camera.Height = 200;
  Screen.Viewport.Width = 960;
  Screen.Viewport.Height = 600;
  SpokenStrings = Dictionary.Create(eSorted, eCaseSensitive);
}

void on_mouse_click (int btn){
  if(btn == eMouseWheelNorth){
    AgsImGui.DoMouseWheel(eImGuiDir_Down);
  } else if(btn == eMouseWheelSouth){
    AgsImGui.DoMouseWheel(eImGuiDir_Up);    
  }  
}

function oObject0_Talk()
{
  player.SayB("Who are you?");
  cNeutralBot.SayB("I am the Neutral Bot!");
}

function hHotspot1_Look()
{
  player.SayB("Lights here are low..."); 
}

function hHotspot2_Look()
{
  player.SayB("If this was a real game, I would pick this rope!");
}

function hHotspot3_Look()
{
  player.SayB("Lots of pipes...");
  cNeutralBot.SayB("Don't have any fuzzy ideas!!!");
  player.SayB("I won't hurt you, they forgot to script this. Or I could...");
}
